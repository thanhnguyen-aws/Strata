/-
  Copyright Strata Contributors

  SPDX-License-Identifier: Apache-2.0 OR MIT
-/
module

public import Strata.DL.Imperative.Cmd
public import Strata.DL.Imperative.EvalError
public import Strata.DL.Imperative.MetaData
public import Strata.DL.Util.ListMap
public import Strata.DL.Util.Maps

namespace Imperative
open Std (ToFormat Format format)

public section

---------------------------------------------------------------------

@[expose] abbrev PathCondition (P : PureExpr)  := ListMap String P.Expr
@[expose] abbrev PathConditions (P : PureExpr) := List (PathCondition P)

def PathCondition.format' {P} [ToFormat P.Expr] (m : PathCondition P) : Format :=
  match m with
  | [] => ""
  | [(k, v)] => f!"({k}, {v})"
  | (k, v) :: rest =>
    f!"({k}, {v})\n" ++ PathCondition.format' rest

def PathConditions.format' {P} [ToFormat P.Expr] (ms : PathConditions P) : Format :=
  match ms with
  | [] => ""
  | [m] => f!"[{PathCondition.format' m}]"
  | m :: rest =>
    f!"[{PathCondition.format' m}]\n" ++
    PathConditions.format' rest

instance [ToFormat P.Expr] : ToFormat (PathCondition P) where
  format p := PathCondition.format' p

instance [ToFormat P.Expr] : ToFormat (PathConditions P) where
  format p := PathConditions.format' p

def PathConditions.newest (ps : PathConditions P) : PathCondition P :=
  match ps with
  | [] => .empty
  | p :: _ => p

def PathConditions.pop (ps : PathConditions P) : PathConditions P :=
  match ps with
  | [] => []
  | _ :: rest => rest

def PathConditions.push (ps : PathConditions P) (p : PathCondition P): PathConditions P :=
  p :: ps

def PathConditions.insert (ps : PathConditions P) (l : String) (e : P.Expr) :=
  match ps with
  | [] => [Map.ofList [(l, e)]]
  | p :: ps => Map.insert p l e :: ps

def PathConditions.addInNewest (ps : PathConditions P) (m : PathCondition P) : PathConditions P :=
  let new := ps.newest ++ m
  let ps := ps.pop
  ps.push new

/-- Remove path conditions with specified names -/
def PathConditions.removeByNames (ps : PathConditions P) (names : List String) : PathConditions P :=
  ps.map (fun pc => pc.filter (fun (name, _) => !names.contains name))

inductive PropertyType where
  | cover
  | assert
  | divisionByZero
  | arithmeticOverflow
  deriving Repr, DecidableEq

/-- Whether an unreachable path counts as pass for this property type.
    Assertions pass vacuously when unreachable; covers fail. -/
def PropertyType.passWhenUnreachable : PropertyType → Bool
  | .assert | .divisionByZero | .arithmeticOverflow => true
  | .cover => false

instance : ToFormat PropertyType where
  format p := match p with
    | .cover => "cover"
    | .assert => "assert"
    | .divisionByZero => "division by zero check"
    | .arithmeticOverflow => "arithmetic overflow check"

/--
A proof obligation can be discharged by some backend solver or a dedicated
decision procedure or via denotation into Lean.
-/
structure ProofObligation (P : PureExpr) where
  label : String
  property : PropertyType
  assumptions : PathConditions P
  obligation : P.Expr
  metadata : MetaData P

instance [BEq P.Ident] [BEq P.Expr] [BEq (MetaData P)] : BEq (ProofObligation P) where
  beq a b :=
    a.label == b.label &&
    a.assumptions == b.assumptions &&
    a.obligation == b.obligation &&
    a.metadata == b.metadata

instance [ToFormat P.Ident] [ToFormat P.Expr] : ToFormat (ProofObligation P) where
  format ob := f!"Label: {ob.label}\n\
                  Property : {ob.property}\n\
                  Assumptions: {PathConditions.format' ob.assumptions}\n\
                  Obligation: {ob.obligation}\n\
                  Metadata: {ob.metadata}\n"

@[expose] abbrev ProofObligations (P : PureExpr) := Array (ProofObligation P)

---------------------------------------------------------------------

class EvalContext (P : PureExpr) (State : Type) where
  /- Evaluation utilities -/
  eval              : State → P.Expr → P.Expr
  updateError       : State → EvalError P → State
  lookupError       : State → Option (EvalError P)
  update            : State → P.Ident → P.Ty → P.Expr → State
  lookup            : State → P.Ident → Option P.TypedExpr
  preprocess        : State → Cmd P → P.Expr → (P.Expr × State)
  genFreeVar        : State → P.Ident → P.Ty → (P.Expr × State)
  denoteBool        : P.Expr → Option Bool

  addWarning        : State → EvalWarning P → State

  /- Path conditions and proof obligations collected during evaluation -/
  getPathConditions : State → (PathConditions P)
  addPathCondition  : State → (PathCondition P) → State
  deferObligation   : State → (ProofObligation P) → State

  -- /-- If two states give the same result to all `lookup` calls, they also
  -- give the same result to all `eval` calls. -/
  -- lookupEval : (s1 : State) → (s2 : State) → (∀ x, lookup s1 x = lookup s2 x) →
  --              (∀ e, eval s1 e = eval s2 e)

---------------------------------------------------------------------

end -- public section
end Imperative
