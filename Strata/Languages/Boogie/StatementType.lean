/-
  Copyright Strata Contributors

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
-/



import Strata.Languages.Boogie.Statement
import Strata.Languages.Boogie.CmdType
import Strata.Languages.Boogie.Program
import Strata.Languages.Boogie.OldExpressions
import Strata.DL.Imperative.CmdType

namespace Boogie
namespace Statement

open Lambda Imperative
open Std (ToFormat Format format)
---------------------------------------------------------------------

/--
Type checker for Boogie commands.

Note that this function needs the entire program to type-check `call`
commands by looking up the corresponding procedure's information.
-/
def typeCheckCmd (T : (TEnv BoogieIdent)) (P : Program) (c : Command) :
  Except Format (Command × (TEnv BoogieIdent)) := do
  match c with
  | .cmd c =>
    let (c, T) ← Imperative.Cmd.typeCheck T c
    .ok (.cmd c, T)
  | .call lhs pname args md =>
     match Program.Procedure.find? P pname with
     | none => .error f!"[{c}]: Procedure {pname} not found!"
     | some proc =>
       if lhs.any (fun l => (T.context.types.find? l).isNone) then
         .error f!"[{c}]: All the return variables {lhs} must exist in the context!"
       else if lhs.length != proc.header.outputs.length then
         .error f!"[{c}]: Arity mismatch in this call's return values!\
                   Here is the expected signature: {proc.header.inputs}"
       else if args.length != proc.header.inputs.length then
         .error f!"[{c}]: Arity mismatch in this call's arguments!\
                   Here is the expected signature: {proc.header.inputs}"
       else do
         -- Get the types of lhs variables and unify with the procedures'
         -- return types.
         let lhsinsts ← Lambda.Identifier.instantiateAndSubsts lhs T
         match lhsinsts with
         | none => .error f!"Implementation error. \
                             Types of {lhs} should have been known."
         | some (lhs_tys, T) =>
           let _ ← T.freeVarChecks args
           let (ret_sig, T) ← LMonoTySignature.instantiate T proc.header.typeArgs proc.header.outputs
           let ret_mtys := LMonoTys.subst T.state.subst ret_sig.values
           let constraints := lhs_tys.zip ret_mtys
           let S ← Constraints.unify constraints T.state.subst
           let T := T.updateSubst S
           -- Infer the types of the actuals and unify with the types of the
           -- procedure's formals.
           let (argsa, T) ← Lambda.LExprT.fromLExprs T args
           let args_tys := argsa.map LExprT.toLMonoTy
           let args' := argsa.map $ LExprT.toLExpr
           let (inp_sig, T) ← LMonoTySignature.instantiate T proc.header.typeArgs proc.header.inputs
           let inp_mtys := LMonoTys.subst T.state.subst inp_sig.values
           let constraints := args_tys.zip inp_mtys
           let S ← Constraints.unify constraints T.state.subst
           let T := T.updateSubst S
           let s' := .call lhs pname args' md
           .ok (s', T)


def typeCheckAux (T : (TEnv BoogieIdent)) (P : Program) (op : Option Procedure) (ss : List Statement) :
  Except Format (List Statement × (TEnv BoogieIdent)) :=
  match ss with
  | [] => .ok (ss, T)
  | s :: srest => do
    let (s', T) ←
      match s with
      | .cmd cmd => do
        let (c', T) ← typeCheckCmd T P cmd
        .ok (.cmd c', T)

      | .block label blk md => do
        let T := T.pushEmptyContext
        let (ss', T) ← typeCheckAux T P op blk.ss
        let s' := .block label ⟨ss'⟩ md
        .ok (s', T.popContext)

      | .ite cond thenb elseb md => do
        let _ ← T.freeVarCheck cond f!"[{s}]"
        let (conda, T) ← LExprT.fromLExpr T cond
        let condty := conda.toLMonoTy
        match condty with
        | .tcons "bool" [] =>
          let (tb, T) ← typeCheckAux T P op [(.block "$$_then" thenb #[])]
          let (eb, T) ← typeCheckAux T P op [(.block "$$_else" elseb #[])]
          let s' := .ite conda.toLExpr ⟨tb⟩ ⟨eb⟩ md
          .ok (s', T)
        | _ => .error f!"[{s}]: If's condition {cond} is not of type `bool`!"

      | .goto label _ =>
        match op with
        | .some p =>
          if Stmts.hasLabelInside label p.body then
            .ok (s, T)
          else
            .error f!"Label {label} does not exist in the body of {p.header.name}"
        | .none => .error f!"{s} occurs outside a procedure."

    let (ss', T) ← typeCheckAux T P op srest
    .ok (s' :: ss', T)
  termination_by Stmts.sizeOf ss
  decreasing_by
  all_goals simp_wf <;> omega

/--
Apply type substitution `S` to a command.
-/
def Command.subst (S : Subst) (c : Command) : Command :=
  match c with
  | .cmd c => match c with
    | .init x ty e md =>
      .cmd $ .init x (LTy.subst S ty) (e.applySubst S) md
    | .set x e md =>
      .cmd $ .set x (e.applySubst S) md
    | .havoc _ _ => .cmd $ c
    | .assert label b md =>
      .cmd $ .assert label (b.applySubst S) md
    | .assume label b md =>
      .cmd $ .assume label (b.applySubst S) md
  | .call lhs pname args md =>
    .call lhs pname (args.map (fun a => a.applySubst S)) md

/--
Apply type substitution `S` to a statement.
-/
def Statement.subst (S : Subst) (s : Statement) : Statement :=
  match s with
  | .cmd cmd => .cmd (Command.subst S cmd)
  | .block label b md =>
    .block label ⟨go S b.ss⟩ md
  | .ite cond thenb elseb md =>
    .ite (cond.applySubst S) ⟨go S thenb.ss⟩ ⟨go S elseb.ss⟩ md
  | .goto _ _ => s
  termination_by (Stmt.sizeOf s)
  decreasing_by
    all_goals simp_wf <;> omega
  where
    go S ss : List Statement :=
    match ss with
    | [] => []
    | s :: srest => Statement.subst S s :: go S srest
    termination_by (Stmts.sizeOf ss)
    decreasing_by
      all_goals simp_wf <;> omega

/--
Type checker and annotater for Statements.

Note that this function needs the entire program to type-check statements to
check whether `goto` targets exist (or .none for statements that don't occur
inside a procedure).
-/
def typeCheck (T : Expression.TyEnv) (P : Program) (op : Option Procedure) (ss : List Statement) :
  Except Format (List Statement × Expression.TyEnv) := do
  let (ss', T) ← typeCheckAux T P op ss
  let context := TContext.subst T.context T.state.subst
  let T := { T with context := context }
  let ss' := Statement.subst.go T.state.subst ss'
  .ok (ss', T)

---------------------------------------------------------------------
end Statement
end Boogie
