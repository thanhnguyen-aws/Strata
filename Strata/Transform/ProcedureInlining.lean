/-
  Copyright Strata Contributors

  SPDX-License-Identifier: Apache-2.0 OR MIT
-/
module

import Strata.DL.Util.LabelGen
import Strata.DL.Util.ListUtils
import Strata.Languages.Core.Core
import Strata.Languages.Core.CoreGen
import Strata.Languages.Core.ProgramWF
public import Strata.Languages.Core.Statement
public import Strata.Transform.CoreTransform
public import Strata.Languages.Core.PipelinePhase
import Strata.Util.Tactics

/-! # Procedure Inlining Transformation -/

public section

namespace Core
namespace ProcedureInlining

open Transform

/-- Statistics keys tracked by the procedure inlining transformation. -/
inductive Stats where
  | visitedCalls
  | inlinedCalls

#derive_prefixed_toString Stats "ProcedureInlining"

-- Gathers all labels including those in assert and assume.
mutual
def Block.labels (b : Block): List String :=
  List.flatMap (fun s => Statement.labels s) b

def Statement.labels (s : Core.Statement) : List String :=
  match s with
  | .block lbl b _ => lbl :: (Block.labels b)
  | .ite _ thenb elseb _ => (Block.labels thenb) ++ (Block.labels elseb)
  | .loop _ _ _ body _ => Block.labels body
  | .assume lbl _ _ => [lbl]
  | .assert lbl _ _ => [lbl]
  | .cover lbl _ _ => [lbl]
  | .exit _ _ => []
  -- No other labeled commands.
  | .cmd _ => []
  | .funcDecl _ _ => []
  | .typeDecl _ _ => []
end

mutual
def Block.replaceLabels (b : Block) (map:Map String String)
    : Block :=
  b.map (fun s => Statement.replaceLabels s map)

def Statement.replaceLabels
    (s : Core.Statement) (map:Map String String) : Core.Statement :=
  let app (s:String) :=
    match Map.find? map s with
    | .none => s
    | .some s' => s'
  match s with
  | .block lbl b m => .block (app lbl) (Block.replaceLabels b map) m
  | .exit lbl m => .exit (lbl.map app) m
  | .ite cond thenb elseb m =>
    .ite cond (Block.replaceLabels thenb map) (Block.replaceLabels elseb map) m
  | .loop g measure inv body m =>
    .loop g measure inv (Block.replaceLabels body map) m
  | .assume lbl e m => .assume (app lbl) e m
  | .assert lbl e m => .assert (app lbl) e m
  | .cover lbl e m => .cover (app lbl) e m
  | .cmd _ => s
  | .funcDecl _ _ => s
  | .typeDecl _ _ => s
end


private def genOldToFreshIdMappings (old_vars : List Expression.Ident)
    (prev_map : Map Expression.Ident Expression.Ident) (prefix_ : String)
    : CoreTransformM (Map Expression.Ident Expression.Ident) := do
  let prev_map <- old_vars.foldlM
    (fun var_map id => do
      let new_name <- genIdent id (fun s => prefix_ ++ "_" ++ s)
      return var_map.insert id new_name)
    prev_map
  return prev_map

private def renameAllLocalNames (c:Procedure)
    : CoreTransformM (Procedure × Map Expression.Ident Expression.Ident) := do
  let var_map: Map Expression.Ident Expression.Ident := []
  let proc_name := c.header.name.name

  -- Make a map for renaming local variables
  let lhs_vars := List.flatMap (fun (s:Statement) => s.definedVars) c.body
  let lhs_vars := lhs_vars ++ c.header.inputs.unzip.fst ++
                  c.header.outputs.unzip.fst
  let var_map <- genOldToFreshIdMappings lhs_vars var_map proc_name

  -- Make a map for renaming label names
  let labels := List.flatMap (fun s => Statement.labels s) c.body
  -- Reuse genOldToFreshIdMappings by introducing dummy data to Identifier
  let label_ids:List Expression.Ident := labels.map
      (fun s => { name:=s, metadata := () })
  let label_map_id <- genOldToFreshIdMappings label_ids [] proc_name
  let label_map := label_map_id.map (fun (id1,id2) => (id1.name, id2.name))

  -- Do substitution
  -- Iterated substitution is safe here: each replacement is a fresh `.fvar` generated
  -- by genOldToFreshIdMappings (counter-based), so a fresh new_id cannot collide with
  -- a later old_id. The iteration is intentionally sequential because each step also
  -- renames LHS variables and labels.
  let new_body := List.map (fun (s0:Statement) =>
    var_map.foldl (fun (s:Statement) (old_id,new_id) =>
        let s := Statement.substFvar s old_id (.fvar () new_id .none)
        let s := Statement.renameLhs s old_id new_id
        Statement.replaceLabels s label_map)
      s0) c.body
  let new_header := { c.header with
    inputs := c.header.inputs.map (fun (id,ty) =>
      match var_map.find? id with
      | .some id' => (id',ty)
      | .none => panic! "unreachable"),
    outputs := c.header.outputs.map (fun (id,ty) =>
      match var_map.find? id with
      | .some id' => (id',ty)
      | .none => panic! "unreachable")
    }
  return ({ c with body := new_body, header := new_header }, var_map)


/-- Update the call graph after inlining one f(caller) -> g(callee) invocation. -/
def updateCallGraph (cg:CallGraph) (f: String) (g: String):
    Except Err CallGraph := do
  -- For each edge 'g -> x', add f -> x'
  let edges_from_g ← match cg.callees.get? g with
    | .some r => .ok r
    | .none => throw s!"Invalid CallGraph: can't find {g} from callees domain"
  let edges_from_f ← match cg.callees.get? f with
    | .some r => .ok r
    | .none => throw s!"Invalid CallGraph: can't find {f} from callees domain"
  let edges_from_f := edges_from_g.fold
    (fun (edges_from_f:Std.HashMap String Nat) fn_x cnt =>
      edges_from_f.alter fn_x (fun v =>
        .some (match v with | .none => cnt | .some v' => cnt + v')))
    edges_from_f
  let callees_new := cg.callees.insert f edges_from_f

  -- Now the callers. For every 'g -> x' edge, add f -> x'.
  let callers_new ← edges_from_g.foldM
    (fun (m:Std.HashMap String (Std.HashMap String Nat)) fn_x cnt => do
      match m.get? fn_x with
      | .none => throw s!"Invalid CallGraph: can't find {fn_x} from callers domain"
      | .some edges_to_x =>
        .ok (m.insert fn_x (edges_to_x.alter f (fun v =>
          .some (match v with | .none => cnt | .some v' => cnt + v')))))
    cg.callers

  let cg_new : CallGraph := { callees := callees_new, callers := callers_new }

  -- .. and decrement the 'f -> g' edge by 1.
  let cg_final ← cg_new.decrementEdge f g
  return cg_final

/-! ### Update assertion metadata with call site information -/

-- Update assertions and assumes in inlined body to carry the call site metadata
-- as the primary file range, moving the original assertion's file range to
-- relatedFileRange.
mutual
def Block.setCallSiteMetadata (b : Block) (callMd : Imperative.MetaData Expression)
    : Block :=
  b.map (fun s => Statement.setCallSiteMetadata s callMd)

def Statement.setCallSiteMetadata (s : Statement) (callMd : Imperative.MetaData Expression)
    : Statement :=
  match s with
  | .cmd (.cmd (.assert lbl e md)) =>
    .assert lbl e (md.setCallSiteFileRange callMd)
  | .cmd (.cmd (.assume lbl e md)) =>
    .assume lbl e (md.setCallSiteFileRange callMd)
  | .cmd (.cmd (.cover lbl e md)) =>
    .cover lbl e (md.setCallSiteFileRange callMd)
  | .block lbl b md =>
    .block lbl (Block.setCallSiteMetadata b callMd) md
  | .ite cond thenb elseb md =>
    .ite cond (Block.setCallSiteMetadata thenb callMd)
              (Block.setCallSiteMetadata elseb callMd) md
  | .loop g measure inv body md =>
    .loop g measure inv (Block.setCallSiteMetadata body callMd) md
  | _ => s
end

/-
Procedure Inlining.

If st is a call statement, inline the contents of the callee procedure.
To avoid conflicts between duplicated variable names in caller and callee,
every variables in callee are renamed.
This function does not update the specification because inlineCallStmt will not
use the specification. This will have to change if Strata also wants to support
the reachability query.
-/
def inlineCallCmd
    (doInline: Option String -> String -> CachedAnalyses -> Bool := λ _caller _callee _analyses => true)
    (cmd: Command)
  : CoreTransformM (Option (List Statement)) :=
    open Lambda in do
    match cmd with
      | .call procName callArgs md =>
        let lhs := CallArg.getLhs callArgs
        let args := CallArg.getInputExprs callArgs
        incrementStat s!"{Stats.visitedCalls}"

        let st ← get
        if ¬ doInline st.currentProcedureName procName st.cachedAnalyses then return .none else
        incrementStat s!"{Stats.inlinedCalls}"

        let some p := (← get).currentProgram
          | throw s!"currentProgram not set"
        let some currProcName := (← get).currentProcedureName
          | throw s!"currentProcedure not set"
        let some proc := Program.Procedure.find? p procName
          | throw s!"Procedure {procName} not found in program"

        -- Create a copy of the procedure that has all input/output/local vars
        -- replaced with fresh ones
        let (proc,var_map) <- renameAllLocalNames proc

        let sigOutputs := LMonoTySignature.toTrivialLTy proc.header.outputs
        let sigInputs := LMonoTySignature.toTrivialLTy proc.header.inputs

        -- Stuffs for the call statement:
        --   call x1,x2, .. = f(v1,v2,...)
        --   where 'procedure f(in1,in2,..) outputs(out1,out2,..)'
        -- Insert
        --   init in1 : ty := v1     --- inputInit
        --   init in2 : ty := v2
        --   init out1 : ty := nondet --- outputInit
        --   init out2 : ty := nondet
        --   ... (f body)
        --   set x1 := out1    --- outputSetStmts
        --   set x2 := out2
        -- `init outN` is not necessary because calls are only allowed to use
        -- already declared variables (per Core.typeCheck)

        -- Declare each renamed output parameter with a nondet init.
        -- No havoc is needed since nondet already gives an
        -- unconstrained value.
        let outputTrips ← genOutExprIdentsTrip sigOutputs sigOutputs.unzip.fst
        let outputInits := outputTrips.map (fun ((_, ty), orgvar) =>
          Statement.init orgvar ty .nondet md)
        -- Create a var statement for each procedure input arguments.
        -- The input parameter expression is assigned to these new vars.
        --let inputTrips ← genArgExprIdentsTrip sigInputs args
        let inputInits := createInits (sigInputs.zip args) md
        -- Assign the output variables in the signature to the actual output
        -- variables used in the callee.
        let outputSetStmts :=
          let out_vars := sigOutputs.unzip.fst
          let out_vars := out_vars.map
              (fun id => match var_map.find? id with
                  | .none => id | .some x => x)
          let outs_lhs_and_sig := List.zip lhs out_vars
          List.map
            (fun (lhs_var,out_var) =>
              Statement.set lhs_var (.fvar () out_var (.none)) md)
            outs_lhs_and_sig

        let stmts:List (Imperative.Stmt Core.Expression Core.Command)
          := inputInits ++ outputInits
             ++ Block.setCallSiteMetadata proc.body md
             ++ outputSetStmts

        -- Update CallGraph if available
        let σ ← get
        match σ.cachedAnalyses.callGraph with
        | .none => modify id -- do nothing
        | .some callGraph =>
          let callGraph' ← updateCallGraph callGraph currProcName procName
          set ({ σ with
            cachedAnalyses := {
              callGraph := .some callGraph'
            }
          }:CoreTransformState)

        return .some [.block (procName ++ "$inlined") stmts md]

      | _ => return .none

end ProcedureInlining

/-- A `doInline` predicate that refuses to inline procedures involved in
    recursion (i.e., part of a cycle in the call graph).  Falls back to
    `true` when no call graph is available. -/
def doInlineNonRecursive (callee : String) (analyses : Transform.CachedAnalyses) : Bool :=
  match analyses.callGraph with
  | none => true
  | some cg => !cg.isRecursive callee

/--
Options to control the behavior of inlining procedure calls in a Core program.
-/
structure InlineTransformOptions where
  -- 'doInline caller callee cachedAnalysis' returns true if the call command
  -- from caller to callee should be inlined. The caller can be none if the
  -- command is an orphaned one (rare, but can happen if inlineCallCmd is run
  -- directly on Command).
  doInline : Option String → String → Transform.CachedAnalyses → Bool :=
      fun _ callee analyses => doInlineNonRecursive callee analyses
  maxIters : Option Nat := none

/-- Procedure-inlining pipeline phase: the transform inlines procedure bodies
    at call sites. Inlining is semantics-preserving, so models are always
    sound (model-preserving).
    - `maxIters = none`: repeat until a fixed point (no changes).
    - `maxIters = some n`: run up to `n` iterations, stopping early if no change. -/
def procedureInliningPipelinePhase
    (opts : InlineTransformOptions := {})
    : PipelinePhase :=
  open Transform in
  modelPreservingPipelinePhase "ProcedureInlining" fun prog =>
    runProgramUntil (ProcedureInlining.inlineCallCmd (doInline := opts.doInline)) prog opts.maxIters

end Core

end -- public section
